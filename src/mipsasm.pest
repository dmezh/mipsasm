program = _{ line+ }
line = { (label ~ ":")? ~ instruction ~ (eol_comment?) ~ "\n" }
instruction = _{
      reg_3_arith_instruction
    | imm_instruction
    | br_instruction
    | j_instruction
    | mem_instruction
}

reg_3_arith_instruction = { (reg_3_arith_opcode ~ register ~ "," ~ register ~ "," ~ register)       }
imm_instruction         = { (imm_opcode         ~ register ~ "," ~ register ~ "," ~ immediate)      }
br_instruction          = { (br_opcode          ~ register ~ "," ~ register ~ "," ~ label)          }
j_instruction           = { (j_opcode           ~ label)                                            }
mem_instruction         = { (mem_opcode         ~ register ~ "," ~ mem_addr ~ "(" ~ register ~ ")") }

reg_3_arith_opcode = _{ op_add | op_or | op_and | op_slt | op_sub }
imm_opcode         = _{ op_addi }
br_opcode          = _{ op_beq }
j_opcode           = _{ op_j }
mem_opcode         = _{ op_sw | op_lw }

immediate = { ASCII_DIGIT+ }
label = { ASCII_ALPHA+ }
mem_addr = { ASCII_DIGIT+ }
register = _{ "$" ~ reg_number }
reg_number = { ('1'..'2' ~ ASCII_DIGIT) | "31" | ASCII_DIGIT }

op_add  = { "add" }
op_or   = { "or" }
op_and  = { "and" }
op_slt  = { "slt" }
op_sub  = { "sub" }
op_addi = { "addi" }
op_beq  = { "beq" }
op_j    = { "j" }
op_sw   = { "sw" }
op_lw   = { "lw" }

eol_comment = _{ "#" ~ (!"\n" ~ ANY)* }
WHITESPACE = _{ " " }
